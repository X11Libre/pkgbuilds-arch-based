# Maintainer: artist for XLibre

pkgname=xlibre-xf86-video-savage
_pkgname=xf86-video-savage
pkgver=2.4.1.2
pkgrel=1
pkgdesc="XLibre fork of X.Org savage drivers"
arch=(x86_64)
license=('custom')
url="https://github.com/X11Libre"
depends=('glibc')
makedepends=('xlibre-xserver-devel' 'X-ABI-VIDEODRV_VERSION=28.0'
             'libdrm' 'xorgproto' 'mesa')
conflicts=('xorg-server<21.1.1' 'X-ABI-VIDEODRV_VERSION<28' 'X-ABI-VIDEODRV_VERSION>=29')
provides=($_pkgname)
groups=('xlibre-drivers')
options=('!emptydirs' '!libtool')
source=("${url}/${_pkgname}/archive/refs/tags/${pkgname}-${pkgver}.tar.gz")
#https://github.com/X11Libre/xf86-video-savage/archive/refs/tags/xlibre-xf86-video-savage-2.4.1.2.tar.gz

prepare() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ${_pkgname}-${pkgname}-${pkgver}

  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  case "$CARCH" in
    "x86_64")
      CFLAGS=" -march=x86-64"
      ;;
    "aarch64")
      CFLAGS=" -march=armv8-a"
      ;;
    *)
      CFLAGS=" -march=native"
      ;;
  esac
  CFLAGS+=" -mtune=generic -O2 -pipe -fexceptions -Wp,-D_FORTIFY_SOURCE=3 -Wformat -Werror=format-security"
  CFLAGS+=" -fstack-clash-protection -fno-omit-frame-pointer -mno-omit-leaf-frame-pointer"
  LDFLAGS=" -Wl,-O1 -Wl,--sort-common -Wl,--as-needed -Wl,-z,lazy -Wl,-z,relro -Wl,-z,pack-relative-relocs"
  if [[ $CARCH != 'aarch64' ]]; then
    CFLAGS+=" -fcf-protection"
  fi
  if [[ "$pkgname" == *"xf86-input"* ]]; then
    CFLAGS+=" -fno-plt"
    LDFLAGS+=" -Wl,-z,now"
  fi
  if [[ "$pkgname" == *"xf86-video-intel"* ]]; then
    CFLAGS+=" -fno-lto"
    LDFLAGS+=" -fno-lto"
  fi
  CXXFLAGS="${CFLAGS} -Wp,-D_GLIBCXX_ASSERTIONS"
  export CFLAGS="${CFLAGS}"
  export CXXFLAGS="${CXXFLAGS}"
  export LDLAGS="${LDLAGS}"

  ./configure --prefix=/usr
  make
}

check() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  make check
}

package() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

sha256sums=('09c5c51c655ce55644d172c8aa2bd50bb3fbb5439fd0e8d4a51890afecf2a487')
