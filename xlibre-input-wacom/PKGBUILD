# Maintainer: artist for XLibre <artist4xlibre@proton.me>

pkgname=xlibre-input-wacom
_pkgname=xf86-input-wacom
pkgver=1.2.3.2
pkgrel=1
pkgdesc="XLibre fork of X.Org Wacom tablet driver"
arch=(x86_64 aarch64)
url="https://github.com/X11Libre"
license=('GPL-2.0-or-later')
depends=('libxi' 'libxinerama' 'libxrandr' 'libx11' 'glibc')
makedepends=('xlibre-xserver' 'xlibre-xserver-common' 'xlibre-xserver-devel' 'X-ABI-XINPUT_VERSION=26.0'
  'xorgproto' 'autoconf' 'automake' 'binutils' 'fakeroot' 'findutils' 'gawk' 'gcc' 'gettext'
  'gzip' 'libtool' 'm4' 'make' 'patch' 'pkgconf' 'meson' 'gobject-introspection')
  # for tests:
  #'python-libevdev' 'python-pytest' 'python-yaml' 'python-gobject' 'python-attrs'
conflicts=("xlibre-${_pkgname}" "$_pkgname" 'X-ABI-XINPUT_VERSION<26' 'X-ABI-XINPUT_VERSION>=27')
replaces=("xlibre-${_pkgname}")
provides=("xlibre-${_pkgname}" "$_pkgname")
source=("${url}/${_pkgname}/archive/refs/tags/xlibre-${_pkgname}-${pkgver}.tar.gz")
groups=('xlibre-drivers')
options=('!debug')

build() {
  case "$CARCH" in
    "x86_64")
      CFLAGS=" -march=x86-64"
      ;;
    "aarch64")
      CFLAGS=" -march=armv8-a"
      ;;
    *)
      CFLAGS=" -march=native"
      ;;
  esac
  CFLAGS+=" -mtune=generic -O2 -pipe -fexceptions -Wp,-D_FORTIFY_SOURCE=3 -Wformat -Werror=format-security"
  CFLAGS+=" -fstack-clash-protection -fno-omit-frame-pointer -mno-omit-leaf-frame-pointer"
  LDFLAGS=" -Wl,-O1 -Wl,--sort-common -Wl,--as-needed -Wl,-z,lazy -Wl,-z,relro -Wl,-z,pack-relative-relocs"
  if [[ $CARCH != 'aarch64' ]]; then
    CFLAGS+=" -fcf-protection"
  fi
  if [[ "$_pkgname" == *"xf86-input"* ]]; then
    CFLAGS+=" -fno-plt"
    LDFLAGS+=" -Wl,-z,now"
  fi
  if [[ "$_pkgname" == *"xf86-video-intel"* ]]; then
    CFLAGS+=" -fno-lto"
    LDFLAGS+=" -fno-lto"
  fi
  CXXFLAGS="${CFLAGS} -Wp,-D_GLIBCXX_ASSERTIONS"
  export CFLAGS="${CFLAGS}"
  export CXXFLAGS="${CXXFLAGS}"
  export LDFLAGS="${LDFLAGS}"

  meson ${_pkgname}-xlibre-${_pkgname}-${pkgver} build \
    --prefix=/usr \
    --libexecdir=lib \
    -D xorg-conf-dir=/usr/share/X11/xorg.conf.d/ \
    -D unittests=disabled

  meson configure build
  ninja -C build
}

#check() {
#  meson test -C build
#}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -r "$pkgdir/usr/lib/systemd"
}

sha256sums=('8c1942cbf90ee80d6505d115bab27461680c623d7acddf6be931224ea8176c36')

